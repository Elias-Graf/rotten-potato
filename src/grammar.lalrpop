use crate::ast::ParseError;
use crate::ast::atom::Atom;
use crate::ast::binary_operation::BinaryOperation;
use crate::ast::binary_operation::BinaryOperator;
use crate::ast::comparison_operation::ComparisonOperation;
use crate::ast::comparison_operation::ComparisonOperator;
use crate::ast::def_listen::DefListen;
use crate::ast::def_listen::DefListenArg;
use crate::ast::def_poll::DefPoll;
use crate::ast::def_poll::DefPollArg;
use crate::ast::def_var::DefVar;
use crate::ast::def_widget::DefWidget;
use crate::ast::def_widget::DefWidgetChild;
use crate::ast::def_widget::DefWidgetParam;
use crate::ast::def_window::DefWindow;
use crate::ast::def_window::DefWindowArg;
use crate::ast::def_window::DefWindowArgValue;
use crate::ast::def_window::DefWindowContent;
use crate::ast::expr::PrimitiveExpr;
use crate::ast::function_call::FunctionCall;
use crate::ast::include::Include;
use crate::ast::literal::Literal;
use crate::ast::literal::LiteralArg;
use crate::ast::symbol::Symbol;
use crate::ast::ternary_operation::TernaryOperation;
use crate::ast::top_level_expr::TopLevelExpr;
use crate::ast::unary_operation::UnaryOperation;
use crate::ast::widget_call::WidgetCall;
use crate::ast::widget_call::WidgetCallArg;
use crate::ast::widget_call::WidgetCallChild;
use crate::lexer::LexicalError;
use crate::lexer::tok::Tok;
use crate::spanned::Spanned;

grammar<'inp, 'err>(errors: &'err mut Vec<ParseError>);

pub TopLevel: Vec<Spanned<TopLevelExpr>> = TopLevelExpr*;

pub TopLevelExpr: Spanned<TopLevelExpr> = {
    DefListen,
    DefPoll,
    DefVar,
    DefWidget,
    DefWindow,
    Include,
    Literal,
}

pub DefListen: Spanned<TopLevelExpr> = {
    <l:@L> "(" "deflisten" <name:Symbol?> <args:DefListenArg*> <script:"literalString"?> ")" <r:@R> => {
        let Some(name) = name else {
            errors.push(ParseError::ExpectedDefListenName { err_span: (l, r).into() });
            return (l, TopLevelExpr::Err, r).into();
        };
        let Some(script) = script else {
            errors.push(ParseError::ExpectedDefListenScript { err_span: (l, r).into() });
            return (l, TopLevelExpr::Err, r).into();
        };

        (l, DefListen::new(name, args, script.to_owned()).into(), r).into()
    },
};

pub DefListenArg: DefListenArg =
    ":" <name:Symbol> <value:Atom> => DefListenArg::new(name, value);

pub DefPoll: Spanned<TopLevelExpr> = {
    <l:@L> "(" "defpoll" <name:Symbol?> <args:DefPollArg*> <script:"literalString"?> ")" <r:@R> => {
        let Some(name) = name else {
            errors.push(ParseError::ExpectedDefPollName {
                err_span: (l, r).into(),
            });
            return (l, TopLevelExpr::Err, r).into();
        };
        let Some(script) = script else {
            errors.push(ParseError::ExpectedDefPollScript {
                err_span: (l, r).into(),
            });
            return (l, TopLevelExpr::Err, r).into();
        };

        (l, DefPoll::new(name, args, script.to_owned()).into(), r).into()
    },
};

pub DefPollArg: DefPollArg =
    ":" <name:Symbol> <value:Atom> => DefPollArg::new(name, value);

pub DefVar: Spanned<TopLevelExpr> = {
    <l:@L> "(" "defvar" <name:Symbol?> <value:Atom?> ")" <r:@R> => {
        let Some(name) = name else {
            errors.push(ParseError::ExpectedDefVarName {
                err_span: (l, r).into(),
            });
            return (l, TopLevelExpr::Err, r).into();
        };
        let Some(value) = value else {
            errors.push(ParseError::ExpectedDefVarValue {
                err_span: (l, r).into(),
            });
            return (l, TopLevelExpr::Err, r).into();
        };

        (l, DefVar::new(name, value).into(), r).into()
    },
};

pub DefWidget: Spanned<TopLevelExpr> = {
    <l:@L> "(" "defwidget" <name:Symbol?> ")" <r:@R> => {
        let Some(name) = name else {
            errors.push(ParseError::ExpectedDefWidgetName { err_span: (l, r).into() });
            return (l, TopLevelExpr::Err, r).into();
        };

        errors.push(ParseError::ExpectedDefWidgetParams { err_span: (l, r).into() });
        (l, TopLevelExpr::Err, r).into()
    },
    <l:@L> "(" "defwidget" <name:Symbol> "[" <params:DefWidgetParam*> "]" <children:DefWidgetChild*> ")" <r:@R> => {
        (l, DefWidget::new(name, params, children).into(), r).into()
    },
};

pub DefWidgetParam: DefWidgetParam = <o:"?"?> <n:Symbol> => DefWidgetParam::new(n, o.is_some());

pub DefWidgetChild: DefWidgetChild = WidgetCall => <>.into();

pub DefWindow: Spanned<TopLevelExpr> = {
    <l:@L>
    "("
        "defwindow"
        <args:DefWindowArgs>
        <content:DefWindowContentList>
    ")"
    <r:@R> => {
        errors.push(ParseError::ExpectedDefWindowName { err_span: (l, r).into() });
        (l, TopLevelExpr::Err, r).into()
    },
    <l:@L> "("
        "defwindow"
        <name:Symbol>
        <args:DefWindowArgs>
        <content:DefWindowContentList>
    ")" <r:@R> => (l, DefWindow::new(name, args, content).into(), r).into(),
};

pub DefWindowArgs: Vec<DefWindowArg> = <DefWindowArg*>;

pub DefWindowArg: DefWindowArg =
    ":" <name:Symbol> <value:DefWindowArgValue> => DefWindowArg::new(name, value);

pub DefWindowArgValue: DefWindowArgValue = {
    Atom => <>.into(),
    WidgetCall => <>.into(),
};

pub DefWindowContentList: Vec<DefWindowContent> = <DefWindowContent*>;

pub DefWindowContent: DefWindowContent = {
    Atom => DefWindowContent::Atom(<>),
    WidgetCall => DefWindowContent::WidgetCall(<>),
};

pub Include: Spanned<TopLevelExpr> = {
    <l:@L> "(" "include" <path:"literalString"?> ")" <r:@R> => {
        let Some(path) = path else {
            errors.push(ParseError::ExpectedIncludePath { err_span: (l, r).into() });
            return (l, TopLevelExpr::Err, r).into();
        };

        (l, Include::new(path).into(), r).into()
    }
};

pub Literal: Spanned<TopLevelExpr> = <l:@L> "(" "literal" <args:LiteralArg*> ")" <r:@R>
    => (l, Literal::new(args).into(), r).into();

pub LiteralArg: LiteralArg = {
    ":" <name:Symbol> <value:"literalString"> => LiteralArg::new(name, value.to_owned()),
    ":" <name:Symbol> <value:Symbol> => LiteralArg::new(name, value),
};

pub WidgetCall: WidgetCall = {
    "("
        <name:Symbol>
        <args:WidgetCallArg*>
        <children:WidgetCallChild*>
    ")" => {
        WidgetCall::new(
            name,
            args,
            children,
        )
    },
};

pub WidgetCallArg: WidgetCallArg =
    ":" <name:Symbol> <value:Atom> => WidgetCallArg::new(name, value);

pub WidgetCallChild: WidgetCallChild = {
    Atom => <>.into(),
    WidgetCall => <>.into(),
};

pub TernaryOperation: PrimitiveExpr = {
    BinaryOperation => <>.1,
    <cmp:ComparisonOperation> "?" <lhs:TernaryOperation> ":" <rhs:TernaryOperation> => {
        TernaryOperation::new(cmp.1, lhs, rhs).into()
    },
};

pub BinaryOperation: Spanned<PrimitiveExpr> = {
    #[precedence(level="0")]
    ComparisonOperation,

    #[precedence(level="1")] #[assoc(side="left")]
    <l:@L> <lhs:BinaryOperation> "&&" <rhs:BinaryOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::And, rhs).into(), r).into(),
    #[precedence(level="2")] #[assoc(side="left")]
    <l:@L> <lhs:BinaryOperation> "||" <rhs:BinaryOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Or, rhs).into(), r).into(),
};

pub ComparisonOperation: Spanned<PrimitiveExpr> = {
    #[precedence(level="0")]
    MathOperation,

    #[precedence(level="1")] #[assoc(side="left")]
    <l:@L> <lhs:ComparisonOperation> "!=" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Neq, rhs).into(), r).into(),
    <l:@L> <lhs:ComparisonOperation> "<" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Lt, rhs).into(), r).into(),
    <l:@L> <lhs:ComparisonOperation> "<=" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Lte, rhs).into(), r).into(),
    <l:@L> <lhs:ComparisonOperation> "==" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Eq, rhs).into(), r).into(),
    <l:@L> <lhs:ComparisonOperation> ">" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Gt, rhs).into(), r).into(),
    <l:@L> <lhs:ComparisonOperation> ">=" <rhs:ComparisonOperation> <r:@R> =>
        (l, ComparisonOperation::new(lhs, ComparisonOperator::Gte, rhs).into(), r).into(),
};

pub MathOperation: Spanned<PrimitiveExpr> = {
    #[precedence(level="0")]
    UnaryOperation,

    #[precedence(level="1")] #[assoc(side="left")]
    <l:@L> <lhs:MathOperation> "*" <rhs:MathOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Mul, rhs).into(), r).into(),
    <l:@L> <lhs:MathOperation> "/" <rhs:MathOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Div, rhs).into(), r).into(),
    <l:@L> <lhs:MathOperation> "%" <rhs:MathOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Mod, rhs).into(), r).into(),
    #[precedence(level="2")] #[assoc(side="left")]
    <l:@L> <lhs:MathOperation> "+" <rhs:MathOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Add, rhs).into(), r).into(),
    <l:@L> <lhs:MathOperation> "-" <rhs:MathOperation> <r:@R> =>
        (l, BinaryOperation::new(lhs, BinaryOperator::Sub, rhs).into(), r).into(),
};

pub UnaryOperation: Spanned<PrimitiveExpr> = {
    "!" <Atom> => (0, UnaryOperation::new_not(<>).into(), 0).into(),
    <l:@L> <a:Atom> <r:@R> => (l, a.into(), r).into(),
    FunctionCall,
};

pub Atom: Atom = {
    "false" => false.into(),
    "true" => true.into(),
    // Symbol => <>.into(),
    "literalNumber" => Atom::new_number(<>.to_owned()),
    "literalString" => Atom::StrLit(<>.to_owned()),
};

pub FunctionCall: Spanned<PrimitiveExpr> =
    <l:@L> <name:Symbol> "(" <args:CommaSeparated<TernaryOperation>> ")" <r:@R> =>
        (l, FunctionCall::new(name, args).into(), r).into();

pub Symbol: Symbol = "literalSymbol" => Symbol::new(<>);

CommaSeparated<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

extern {
    type Location = usize;
    type Error = LexicalError<'inp>;

    enum Tok<'inp> {
        "literalNumber" => Tok::LiteralNumber(<&'inp str>),
        "literalString" => Tok::LiteralString(<&'inp str>),
        "literalSymbol" => Tok::LiteralSymbol(<&'inp str>),
        "include" => Tok::KeywordInclude,
        "literal" => Tok::KeywordLiteral,
        "deflisten" => Tok::KeywordDefListen,
        "defpoll" => Tok::KeywordDefPoll,
        "defvar" => Tok::KeywordDefVar,
        "defwidget" => Tok::KeywordDefWidget,
        "defwindow" => Tok::KeywordDefWindow,
        "false" => Tok::KeywordFalse,
        "true" => Tok::KeywordTrue,
        "(" => Tok::DelimiterLeftParen,
        ")" => Tok::DelimiterRightParen,
        "[" => Tok::DelimiterLeftBracket,
        "]" => Tok::DelimiterRightBracket,
        "," => Tok::DelimiterComma,
        "*" => Tok::OperatorMathMultiplication,
        "/" => Tok::OperatorMathDivision,
        "%" => Tok::OperatorMathModulo,
        "+" => Tok::OperatorMathAddition,
        "-" => Tok::OperatorMathSubtraction,
        "||" => Tok::OperatorLogicalOr,
        "&&" => Tok::OperatorLogicalAnd,
        "!" => Tok::OperatorLogicalNot,
        "==" => Tok::OperatorComparisonEqual,
        "!=" => Tok::OperatorComparisonNotEqual,
        "<=" => Tok::OperatorComparisonLessThanOrEquals,
        ">=" => Tok::OperatorComparisonGreaterThanOrEquals,
        ">" => Tok::OperatorComparisonGreaterThan,
        "<" => Tok::OperatorComparisonLessThan,
        "?" => Tok::PunctuationQuestionMark,
        ":" => Tok::PunctuationColon,
    }
}

// vim: ft=rust
